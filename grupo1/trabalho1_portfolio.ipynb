{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### COM361 &mdash; Introdução a Otimização &mdash; 2024, Prof. Amit ###\n",
    "\n",
    "# Chance Constrained Portfolio Optimization #\n",
    "\n",
    "#### Wagner Franco da Silva Junior (wagnerjunior@poli.ufrj.br)\n",
    "\n",
    "*****\n",
    "\n",
    "### Índice\n",
    "\n",
    "1. [Introdução](#1.-Introdução)\n",
    "1. [Demonstração](#2.-Demonstração)\n",
    "1. [Formulação](#3.-Formulação)\n",
    "1. [Comparação com Markowitz](#4.-Comparação-com-Markowitz)\n",
    "1. [Conclusão](#5.-Conclusão)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {
    "collapsed": true
   },
   "source": [
    "## 1. Introdução ##\n",
    "\n",
    "Neste trabalho foi abordado e estudado o comportamento de uma carteira de investimentos usando uma restrição probabilística do problema para alocação ótima da carteira, onde o objetivo central foi estabeler um mínimo de retorno $R_{min}$ com um nível de confiança $\\alpha$ para obtenção mais confiável dos dados. Essa formulação é muito útil em situações em que a segurança de um retorno mínimo é indispensável, como em investimentos institucionais ou estratégias conservadoras."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {
    "collapsed": true
   },
   "source": [
    "## 2. Demonstração ##\n",
    "\n",
    "Considerando um LP na forma padrão e uma desigualdade linear incerta/probabilística, temos:\n",
    "\n",
    "$$\n",
    "\\begin{aligned}\n",
    "\\underset{x}{\\text{min}}\\qquad& c^Tx \\\\\n",
    "\\text{sujeito a:}\\qquad& a_i^T x \\le b_i && i=1,\\dots,m\\\\\n",
    "\\end{aligned}\n",
    "$$\n",
    "\n",
    "Onde $a_i$ não é conhecido mas sabemos que obedece a uma distribuição gaussiana com esperança $\\bar{a}$ e matriz de covariância $\\Sigma_i$ positiva definida. E a partir disso podemos provar que $a_i^T x$ também é uma variável aleatória com distribuição gaussiana e sua média e variância podem ser encontradas da seguinte forma:\n",
    "\n",
    "### Cálculo da Esperança\n",
    "\n",
    "$$\n",
    "y = a_i^T x\n",
    "$$\n",
    "\n",
    "A esperança matemática de $ y $ é dada por:\n",
    "\n",
    "$$\n",
    "\\mathbb{E}[y] = \\mathbb{E}[a_i^T x]\n",
    "$$\n",
    "\n",
    "Como  $x$ é um vetor fixo e determinístico, ele pode ser retirado da expectativa. Temos:\n",
    "\n",
    "$$\n",
    "\\mathbb{E}[a_i^T x] = x^T \\mathbb{E}[a_i]\n",
    "$$\n",
    "\n",
    "Sabemos que $ a_i \\sim \\mathcal{N}(\\bar{a}_i, \\Sigma_i) $, portanto, a média de $ a_i $ é $ \\bar{a}_i $. Substituindo:\n",
    "\n",
    "$$\n",
    "\\mathbb{E}[a_i^T x] = x^T \\bar{a}_i\n",
    "$$\n",
    "\n",
    "Por fim:\n",
    "\n",
    "$$\n",
    "\\mathbb{E}[y] = \\bar{a}_i^T x\n",
    "$$\n",
    "\n",
    "### Cálculo da Variância\n",
    "\n",
    "$$\n",
    "Var(y) = Var(a_i^T x)\n",
    "$$\n",
    "\n",
    "Como neste caso, x é um vetor fixo e $ a_i \\sim \\mathcal{N}(\\bar{a}_i, \\Sigma_i) $ pode-se aplicar a fórmula padrão para uma combinação linear de variáveis gaussianas que é:\n",
    "\n",
    "$$\n",
    "Var(a_i^T x) = x^T \\Sigma_i x\n",
    "$$\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {
    "collapsed": true
   },
   "source": [
    "## 3. Formulação ##\n",
    "\n",
    "Para formulação do problema, sabemos que a função objetivo do modelo é para maximizar o retorno esperado com a limitação da probabilidade de perda ao máximo $\\alpha$ e de forma que as alocações de x satisfaça $\\Sigma x_i$ e $x_i \\ge 0$. Assim, basta transformar a restrição em SOCP e utilizar na forma padrão para otimização, da seguinte forma:\n",
    "\n",
    "$$\n",
    "Prob\\{p^T x \\geq R_{\\text{min}}\\} \\geq \\alpha\n",
    "$$\n",
    "\n",
    "onde:\n",
    "- $ p $: Vetor de retornos médios.\n",
    "- $ x $: Vetor de pesos de alocação nos ativos.\n",
    "- $ R_{\\text{min}} $: Retorno mínimo desejado.\n",
    "- $ \\alpha $: Nível de confiança ($ 0 < \\alpha < 1 $).\n",
    "\n",
    "---\n",
    "\n",
    "A probabilidade acumulada é expressa como:\n",
    "\n",
    "$$\n",
    "Prob\\left\\{p^T x - R_{\\text{min}} \\geq \\Phi^{-1}(\\alpha) \\cdot \\sqrt{x^T \\Sigma x}\\right\\} \\geq 0\n",
    "$$\n",
    "\n",
    "$ \\Phi^{-1}(\\alpha) $ é a quantidade da distribuição normal padrão associada ao nível de confiança $ \\alpha $.\n",
    "\n",
    "---\n",
    "\n",
    "A inequação acima pode ser reescrita em termos determinísticos como:\n",
    "\n",
    "$$\n",
    "p^T x - R_{\\text{min}} \\geq \\Phi^{-1}(\\alpha) \\cdot \\sqrt{x^T \\Sigma x}\n",
    "$$\n",
    "\n",
    "Isso pode ser reorganizado para:\n",
    "\n",
    "$$\n",
    "p^T x - \\Phi^{-1}(\\alpha) \\cdot \\sqrt{x^T \\Sigma x} \\geq R_{\\text{min}}\n",
    "$$\n",
    "\n",
    "---\n",
    "Para que o problema possa ser resolvido usando otimização cônica, introduzimos uma variável auxiliar $ t $, tal que:\n",
    "\n",
    "$$\n",
    "t \\geq \\sqrt{x^T \\Sigma x}\n",
    "$$\n",
    "\n",
    "A restrição $ p^T x - \\Phi^{-1}(\\alpha) \\cdot t \\geq R_{\\text{min}} $ é linear em $ t $, e a condição $ t \\geq \\sqrt{x^T \\Sigma x} $ pode ser expressa como uma restrição de cone de segunda ordem:\n",
    "\n",
    "$$\n",
    "\\| \\Sigma^{1/2} x \\|_2 \\leq t\n",
    "$$\n",
    "\n",
    "onde $ \\| \\cdot \\|_2 $ denota a norma $ \\ell_2 $.\n",
    "\n",
    "---\n",
    "Assim, o SOCP final fica da seguinte forma:\n",
    "\n",
    "$$\n",
    "\\begin{aligned}\n",
    "\\underset{x}{\\text{max}}\\qquad& \\bar{p}^T x \\\\\n",
    "\\text{sujeito a:}\\qquad& p^T x - t \\cdot \\| \\Sigma^{1/2} x \\|_2 \\geq R_{\\text{min}},\\\\ \\qquad& \\Sigma x_i = 1, x_i \\geq 0\n",
    "\\end{aligned}\n",
    "$$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Nesse script foi feito o modelo usando médias e variância aleatórias para testar, e foi obtido um retorno esperado de 11.9%\n",
    "using JuMP\n",
    "using COSMO\n",
    "using LinearAlgebra\n",
    "using Random\n",
    "using Distributions\n",
    "\n",
    "n = 5\n",
    "Random.seed!(123)\n",
    "\n",
    "p = rand(0.05:0.01:0.15, n)\n",
    "\n",
    "Σ = Symmetric(rand(0.01:0.01:0.05, n, n))\n",
    "Σ += Diagonal(ones(n)) * 0.1 \n",
    "\n",
    "α = 0.95\n",
    "Φ_inv = quantile(Normal(0, 1), α)\n",
    "R_min = 0.08\n",
    "\n",
    "model = Model(COSMO.Optimizer)\n",
    "\n",
    "@variable(model, x[1:n] >= 0)\n",
    "@variable(model, t >= 0)\n",
    "\n",
    "@objective(model, Max, dot(p, x))\n",
    "\n",
    "@constraint(model, sum(x) == 1)\n",
    "\n",
    "@constraint(model, p'x - Φ_inv * t >= R_min)\n",
    "@constraint(model, [Σ^(1/2) * x; t] in SecondOrderCone())\n",
    "\n",
    "optimize!(model)\n",
    "\n",
    "if termination_status(model) == MOI.OPTIMAL\n",
    "    println(\"Solução ótima encontrada:\")\n",
    "    println(\"Alocação: \", value.(x))\n",
    "    println(\"Retorno esperado: \", dot(p, value.(x)))\n",
    "else\n",
    "    println(\"Falha na obtenção de solução ótima.\")\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {
    "collapsed": true
   },
   "source": [
    "## 4. Comparação com Markowitz ##\n",
    "\n",
    "A comparação com o QP de Markowitz pode ser feita de diversas formas, o seguinte script retorna um valor esperado de 0.327% com um risco de 4.46% que são ligeiramente melhores que o de Markowitz, principalmente ao considerarmos que para o valor esperado chegar perto disso (0.34% no Markowitz) foi necessário colocar um risco relativamente algo no valor de $\\lambda$, de forma que torna inviável correr tanto riscos para ter retorno, que mostra um primeiro ponto a favor do modelo probabilístico, apesar dos riscos grandes do mesmo, mas com um valor de confiabilidade alto isso pode ser resolvido. Além do probabilístico ser mais robusto a incertezas justamente por ser uma parte fundamental do próprio modelo."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using JuMP\n",
    "using COSMO\n",
    "using LinearAlgebra\n",
    "using CSV\n",
    "using DataFrames\n",
    "using Distributions\n",
    "using PyPlot\n",
    "\n",
    "p_df = CSV.read(\"folio_mean.csv\", DataFrame)\n",
    "p = Matrix(p_df)[:, 1]\n",
    "\n",
    "Σ_df = CSV.read(\"folio_cov.csv\", DataFrame)\n",
    "Σ = Matrix(Σ_df[:, 2:end])\n",
    "Σ = Symmetric(Σ)\n",
    "\n",
    "n = length(p)\n",
    "\n",
    "α = 0.95\n",
    "Φ_inv = quantile(Normal(0, 1), α)\n",
    "R_min = 0.0031\n",
    "\n",
    "model = Model(COSMO.Optimizer)\n",
    "\n",
    "@variable(model, x[1:n] >= 0)\n",
    "@variable(model, t >= 0)\n",
    "\n",
    "@objective(model, Max, dot(p, x))\n",
    "@constraint(model, sum(x) == 1)\n",
    "@constraint(model, p'x - Φ_inv * t >= R_min)\n",
    "@constraint(model, [Σ^(1/2) * x; t] in SecondOrderCone())\n",
    "\n",
    "optimize!(model)\n",
    "\n",
    "if termination_status(model) == MOI.OPTIMAL\n",
    "    xsol = value.(x)\n",
    "    ret = dot(p, xsol)\n",
    "    std = sqrt(dot(xsol, Σ * xsol))\n",
    "\n",
    "    figure(figsize=(12, 6))\n",
    "    bar(1:n, xsol, color=\"blue\")\n",
    "    title(string(\"Otimização - Portfolio\\n\",\n",
    "                 \"Retorno esperado (p): \", round(ret * 100, digits=3), \"%, \",\n",
    "                 \"Risco (σ): \", round(std * 100, digits=2), \"%\"))\n",
    "    xlabel(\"Ativos\")\n",
    "    ylabel(\"Alocação\")\n",
    "    xlim(0, n + 1)\n",
    "    tight_layout()\n",
    "    show()\n",
    "else\n",
    "    println(\"Falha na obtenção de solução ótima.\")\n",
    "end\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {
    "collapsed": true
   },
   "source": [
    "## 5. Conclusão ##\n",
    "\n",
    "Dessa forma, podemos concluir que o melhor modelo depende da situação em que vai ser aplicado, se precisar de um modelo mais robusto mas que sofra alguns riscos, o probabilístico se encaixa, caso queira um modelo mais simples que garanta que vai ter retorno dependendo do valor de risco que for aplicado, a escolha é o de Markowitz. A abordagem probabilística, ao incorporar distribuições acumuladas oferece maior proteção a cenários adversos como em mercados voláteis. Os resultados obtidos evidenciam que a probabilística, apesar de mais complexa computacionalmente, proporciona alocações de ativos mais confiáveis, especialmente em contextos onde incertezas e choques de mercado desempenham um papel relevante. Isso reforça sua aplicabilidade em cenários reais, onde retornos frequentemente não seguem distribuições normais e eventos extremos podem impactar significativamente o desempenho dos portfólios"
   ]
  }
 ],
 "metadata": {
  "@webio": {
   "lastCommId": null,
   "lastKernelId": null
  },
  "kernelspec": {
   "display_name": "Julia 1.11.2",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 1
}
